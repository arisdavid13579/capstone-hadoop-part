
-- Temp table used to load the branch table into warehouse
DROP TABLE IF EXISTS CDW_SAPP.TEMPORARY_TABLE_CDW_SAPP_D_BRANCH;
CREATE EXTERNAL TABLE CDW_SAPP.TEMPORARY_TABLE_CDW_SAPP_D_BRANCH( 
	BRANCH_CODE INT,
	BRANCH_NAME VARCHAR(25),
	BRANCH_STREET VARCHAR(30),
	BRANCH_CITY VARCHAR(30),
	BRANCH_STATE VARCHAR(30),
	BRANCH_ZIP VARCHAR(5),
	BRANCH_PHONE VARCHAR(13),
	LAST_UPDATED TIMESTAMP
)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\t' LINES TERMINATED BY '\n'
LOCATION "/Credit_Card_System/CDW_SAPP_D_BRANCH/";

-- Permanent branch table. This table is only created during the script's first run.
CREATE TABLE IF NOT EXISTS CDW_SAPP.CDW_SAPP_D_BRANCH (
	BRANCH_CODE INT,
	BRANCH_NAME VARCHAR(25),
	BRANCH_STREET VARCHAR(30),
	BRANCH_CITY VARCHAR(30),
	BRANCH_ZIP VARCHAR(5),
	BRANCH_PHONE VARCHAR(13),
	LAST_UPDATED TIMESTAMP
)
PARTITIONED BY (BRANCH_STATE VARCHAR(30))
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\t' LINES TERMINATED BY '\n'
STORED AS TEXTFILE;

SET hive.exec.dynamic.partition = true;
SET hive.exec.dynamic.partition.mode = nonstrict;

INSERT ${INSERTION_MODE} TABLE CDW_SAPP.CDW_SAPP_D_BRANCH PARTITION (BRANCH_STATE)
-- The INSERTION_MODE parameter is changed into 'OVERWRITE'  for the non-optimal module or 'INTO' for optimization. 

SELECT
	BRANCH_CODE,
	BRANCH_NAME,
	BRANCH_STREET,
	BRANCH_CITY,
	BRANCH_ZIP,
	BRANCH_PHONE,
	LAST_UPDATED,
	BRANCH_STATE
FROM 
	CDW_SAPP.TEMPORARY_TABLE_CDW_SAPP_D_BRANCH;
