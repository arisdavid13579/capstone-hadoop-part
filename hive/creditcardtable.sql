CREATE DATABASE IF NOT EXISTS CDW_SAPP; -- This database holds all tables. It is created only once.

-- Temp table used to load the credit card table into warehouse
DROP TABLE IF EXISTS CDW_SAPP.TEMPORARY_TABLE_CDW_SAPP_F_CREDIT_CARD;
CREATE EXTERNAL TABLE CDW_SAPP.TEMPORARY_TABLE_CDW_SAPP_F_CREDIT_CARD(
CUST_CC_NO STRING, 
TIMEID VARCHAR(8), 
CUST_SSN INT,
BRANCH_CODE INT,
TRANSACTION_TYPE VARCHAR(30), 
TRANSACTION_VALUE DECIMAL(20,3), 
TRANSACTION_ID INT, 
LAST_UPDATED TIMESTAMP
)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\t' LINES TERMINATED BY '\n'
LOCATION "/Credit_Card_System/CDW_SAPP_F_CREDIT_CARD/";

-- Permanent credit card table.  This table is only created during the script's first run.
CREATE TABLE IF NOT EXISTS CDW_SAPP.CDW_SAPP_F_CREDIT_CARD ( 
CUST_CC_NO STRING, 
TIMEID VARCHAR(8),
CUST_SSN INT,
TRANSACTION_TYPE VARCHAR(30), 
TRANSACTION_VALUE DECIMAL(20,3), 
TRANSACTION_ID INT, 
LAST_UPDATED TIMESTAMP
)
PARTITIONED BY (BRANCH_CODE INT)
ROW FORMAT DELIMITED FIELDS TERMINATED BY '\t' LINES TERMINATED BY '\n'
STORED AS TEXTFILE;

SET hive.exec.dynamic.partition = true;
SET hive.exec.dynamic.partition.mode = nonstrict;

INSERT ${INSERTION_MODE} TABLE CDW_SAPP.CDW_SAPP_F_CREDIT_CARD  PARTITION (BRANCH_CODE)
-- The INSERTION_MODE parameter is changed into 'OVERWRITE'  for the non-optimal module or 'INTO' for optimization. 

SELECT 
	CUST_CC_NO,
	TIMEID,
	CUST_SSN,
	TRANSACTION_TYPE,
	TRANSACTION_VALUE,
	TRANSACTION_ID,
	LAST_UPDATED,
	BRANCH_CODE
FROM CDW_SAPP.TEMPORARY_TABLE_CDW_SAPP_F_CREDIT_CARD;

